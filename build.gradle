apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Augmented Driver'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Framework on top of Appium/WebDriver for E2E tests for Web, IOS and Android'
        url 'https://github.com/relateiq/AugmentedDriver'

        scm {
          connection 'scm:git:git@github.com:/relateiq/AugmentedDriver.git'
          developerConnection 'scm:git:git@github.com:/relateiq/AugmentedDriver.git'
          url 'git@github.com:/relateiq/AugmentedDriver.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'SenhorCastor'
            name 'Fernando Martin'
            email 'fernando.a.martin@gmail.com'
          }
        }
      }
    }
  }
}


repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

group = "com.salesforceiq.augmenteddriver"
archivesBaseName = "augmenteddriver"
version = "1.1.11"

artifacts {
    archives javadocJar, sourcesJar
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.inject.extensions:guice-assistedinject:4.0'
    compile 'org.seleniumhq.selenium:selenium-java:2.51.0'
    compile 'org.slf4j:slf4j-log4j12:1.7.13'        
    compile 'junit:junit:4.12'
    compile 'com.beust:jcommander:1.48'
    compile 'com.esotericsoftware.yamlbeans:yamlbeans:1.09'
    compile 'com.saucelabs:saucerest:1.0.28'
    compile 'io.appium:java-client:3.3.0'   

    // For ALLURE
    compile 'org.aspectj:aspectjweaver:1.8.8'
    compile 'ru.yandex.qatools.allure:allure-junit-adaptor:1.4.20'
}


jar {
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    baseName = 'AugmentedDriver'
    version = '0.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

